#							-*- Autoconf -*-

cat <<EOF

M4sugar.

EOF

## -------------- ##
## m4_text_wrap.  ##
## -------------- ##

AT_SETUP([[m4_text_wrap]])

# m4_text_wrap is used to display the help strings.  Also, check that
# commas are not swallowed.  This can easily happen because of
# m4-listification.

AT_DATA(configure.in,
[[AC_PLAIN_SCRIPT()dnl
m4_text_wrap([Short string */], [   ], [/* ], 20)

m4_text_wrap([Much longer string */], [   ], [/* ], 20)

m4_text_wrap([Short doc.], [          ], [  --short ], 30)

m4_text_wrap([Short doc.], [          ], [  --too-wide], 30)

m4_text_wrap([Super long documentation.], [          ], [  --too-wide], 30)

m4_text_wrap([First, second  , third, [,quoted]])
]])

AT_DATA(expout,
[[/* Short string */

/* Much longer
   string */

  --short Short doc.

  --too-wide
          Short doc.

  --too-wide
          Super long
          documentation.

First, second , third, [,quoted]
]])

AT_CHECK([autoconf --autoconf-dir .. -l $at_srcdir -o-], 0, expout)

AT_CLEANUP()



## --------- ##
## m4_warn.  ##
## --------- ##

AT_SETUP([[m4_warn]])

# m4_text_wrap is used to display the help strings.  Also, check that
# commas are not swallowed.  This can easily happen because of
# m4-listification.

AT_DATA(configure.in,
[[m4_warn([foo],  [foo])
m4_warn([bar],    [bar])
m4_warn([syntax], [syntax])
]])

AT_CHECK([autoconf --autoconf-dir .. -l $at_srcdir -o-],
         0, [],
[configure.in:3: warning: syntax
])

AT_CHECK([autoconf --autoconf-dir .. -l $at_srcdir -o- -Wall],
         0, [],
[configure.in:1: warning: foo
configure.in:2: warning: bar
configure.in:3: warning: syntax
])

AT_CHECK([autoconf --autoconf-dir .. -l $at_srcdir -o- -Wnone,bar],
         0, [],
[configure.in:2: warning: bar
])

AT_CHECK([autoconf --autoconf-dir .. -l $at_srcdir -o- -Wnone,bar,error],
         1, [],
[configure.in:2: error: bar
configure.in:2: the top level
])

AT_CLEANUP()
