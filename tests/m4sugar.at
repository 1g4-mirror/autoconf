#							-*- Autoconf -*-

cat <<EOF

M4sugar.

EOF

## -------------- ##
## m4_text_wrap.  ##
## -------------- ##

AT_SETUP(m4_text_wrap)

# m4_text_wrap is used to display the help strings.  Also, check that
# commas are not swallowed.  This can easily happen because of
# m4-listification.

AT_DATA(configure.in,
[[AC_PLAIN_SCRIPT()dnl
m4_text_wrap([Short string */], [   ], [/* ], 20)

m4_text_wrap([Much longer string */], [   ], [/* ], 20)

m4_text_wrap([Short doc.], [          ], [  --short ], 30)

m4_text_wrap([Short doc.], [          ], [  --too-wide], 30)

m4_text_wrap([Super long documentation.], [          ], [  --too-wide], 30)

m4_text_wrap([First, second  , third, [,quoted]])
]])

AT_DATA(expout,
[[/* Short string */

/* Much longer
   string */

  --short Short doc.

  --too-wide
          Short doc.

  --too-wide
          Super long
          documentation.

First, second , third, [,quoted]
]])

AT_CHECK([../autoconf --autoconf-dir .. -l $at_srcdir -o-], 0, expout)

AT_CLEANUP()
