dnl Process this file with autoconf to produce a configure script.
AC_INIT(acgeneral.m4)
AM_INIT_AUTOMAKE(autoconf, 2.14.1)

dnl We use a path for GNU m4 so even if users have another m4 first in
dnl their path, the installer can configure with a path that has GNU m4
dnl first and get that path embedded in the installed autoconf and
dnl autoheader scripts.
AC_PATH_PROGS(M4, gm4 gnum4 m4, m4)
AC_CACHE_CHECK(whether we are using GNU m4, acac_cv_gnu_m4,
[acac_cv_gnu_m4=no
if test x"$M4" != x; then
  case `$M4 --help < /dev/null 2>&1` in
    *traditional*) acac_cv_gnu_m4=yes ;;
  esac
fi])
if test x"$acac_cv_gnu_m4" != xyes; then
  AC_ERROR([GNU m4 is required])
fi
AC_PROG_AWK

dnl We use a path for perl so the #! line in autoscan will work.
AC_PATH_PROG(PERL, perl, no)
AC_SUBST(PERL)dnl
AC_SUBST(PERLSCRIPTS)dnl
if test "$PERL" != no; then
  PERLSCRIPTS=autoscan
else
  AC_MSG_WARN(autoscan will not be built since perl is not found)
fi

AC_PROG_INSTALL

# Work with the GNU or Cygnus source tree layout.
if test -f $srcdir/standards.texi; then
  standards_texi=standards.texi
fi
AC_SUBST(standards_texi)dnl

AC_OUTPUT(acversion.m4 Makefile testsuite/Makefile)
