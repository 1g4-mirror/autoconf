dnl Process this file with autoconf to produce a configure script.
AC_INIT(acgeneral.m4)
AM_INIT_AUTOMAKE(autoconf, 2.14a)

dnl We use a path for GNU m4 so even if users have another m4 first in
dnl their path, the installer can configure with a path that has GNU m4
dnl first and get that path embedded in the installed autoconf and
dnl autoheader scripts.
AC_PROG_GNU_M4
if test x"$ac_cv_prog_gnu_m4" != xyes; then
  AC_MSG_ERROR([GNU m4 is required])
fi
dnl This is needed because Automake does not seem to realize there is
dnl an AC_SUBST inside AC_PROG_GNU_M4.  Grmph!
AC_SUBST(M4)

dnl `autoconf' and `ifnames' use AWK.
AC_PROG_AWK

dnl Generating man pages.
AM_MISSING_PROG(HELP2MAN, help2man)

dnl We use a path for perl so the #! line in autoscan will work.
AC_PATH_PROG(PERL, perl, no)
AC_SUBST(PERL)dnl
AC_SUBST(PERLSCRIPTS)dnl
if test "$PERL" != no; then
  PERLSCRIPTS=autoscan
else
  AC_MSG_WARN(autoscan will not be built since perl is not found)
fi

AC_PROG_INSTALL

# Work with the GNU or Cygnus source tree layout.
if test -f $srcdir/standards.texi; then
  standards_texi=standards.texi
fi
AC_SUBST(standards_texi)dnl

AC_OUTPUT(acversion.m4 Makefile man/Makefile testsuite/Makefile)
