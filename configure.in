# Process this file with autoconf to produce a configure script.
AC_PREREQ(2.49a)
AC_INIT(Autoconf, 2.49b, bug-autoconf@gnu.org)
AC_CONFIG_SRCDIR(acgeneral.m4)
AM_INIT_AUTOMAKE(autoconf, 2.49b)

# Initialize the test suite.
AT_CONFIG(..)

# We use a path for GNU m4 so even if users have another m4 first in
# their path, the installer can configure with a path that has GNU m4
# first and get that path embedded in the installed autoconf and
# autoheader scripts.
AC_PROG_GNU_M4
if test x"$ac_cv_prog_gnu_m4" != xyes; then
  AC_MSG_ERROR([GNU m4 1.4 is required])
fi
# This is needed because Automake does not seem to realize there is
# an AC_SUBST inside AC_PROG_GNU_M4.  Grmph!
AC_SUBST(M4)

# `autoconf' and `ifnames' use AWK.
AC_PROG_AWK

# Generating man pages.
AM_MISSING_PROG(HELP2MAN, help2man, $missing_dir)

# We use a path for perl so the #! line in autoscan will work.
AC_PATH_PROG(PERL, perl, no)
AC_SUBST(PERL)dnl
AC_SUBST(PERLSCRIPTS)dnl
if test "$PERL" != no; then
  PERLSCRIPTS=autoscan
else
  AC_MSG_WARN([autoscan will not be built since perl is not found])
fi

AC_PROG_INSTALL

# Work with the GNU or Cygnus source tree layout.
if test -f $srcdir/standards.texi; then
  AC_SUBST(standards_texi, standards.texi)dnl
fi

AC_OUTPUT(Makefile m4/Makefile man/Makefile doc/Makefile
          tests/Makefile tests/atconfig)
